#!/usr/bin/env python
from misc.utility.scons_hints import *

import os
import platform
import subprocess
import sys

from methods import print_warning

Import("env")

android_files = [
    "os_android.cpp",
    "android_input_handler.cpp",
    "file_access_android.cpp",
    "file_access_filesystem_jandroid.cpp",
    "audio_driver_opensl.cpp",
    "dir_access_jandroid.cpp",
    "tts_android.cpp",
    "thread_jandroid.cpp",
    "net_socket_android.cpp",
    "java_godot_lib_jni.cpp",
    "java_class_wrapper.cpp",
    "java_godot_wrapper.cpp",
    "java_godot_view_wrapper.cpp",
    "java_godot_io_wrapper.cpp",
    "jni_utils.cpp",
    "android_keys_utils.cpp",
    "display_server_android.cpp",
    "plugin/godot_plugin_jni.cpp",
    "rendering_context_driver_vulkan_android.cpp",
    "variant/callable_jni.cpp",
    "dialog_utils_jni.cpp",
    "game_menu_utils_jni.cpp",
]

env_android = env.Clone()

android_objects = []
for x in android_files:
    android_objects.append(env_android.SharedObject(x))

env_thirdparty = env_android.Clone()
env_thirdparty.disable_warnings()
thirdparty_obj = env_thirdparty.SharedObject("#thirdparty/misc/ifaddrs-android.cc")
android_objects.append(thirdparty_obj)

lib = env_android.add_shared_library("#bin/libgodot", [android_objects], SHLIBSUFFIX=env["SHLIBSUFFIX"])

# Needed to force rebuilding the platform files when the thirdparty code is updated.
env.Depends(lib, thirdparty_obj)

lib_arch_dir = ""
triple_target_dir = ""
if env["arch"] == "arm32":
    lib_arch_dir = "armeabi-v7a"
    triple_target_dir = "arm-linux-androideabi"
elif env["arch"] == "arm64":
    lib_arch_dir = "arm64-v8a"
    triple_target_dir = "aarch64-linux-android"
elif env["arch"] == "x86_32":
    lib_arch_dir = "x86"
    triple_target_dir = "i686-linux-android"
elif env["arch"] == "x86_64":
    lib_arch_dir = "x86_64"
    triple_target_dir = "x86_64-linux-android"
elif env["arch"] == "rv64":
    lib_arch_dir = "riscv64"
    triple_target_dir = "riscv64-linux-android"
else:
    print_warning("Architecture not suitable for embedding into APK; keeping .so at \\bin")

# Detect host platform for new NDK structure
host_subpath = ""
if sys.platform.startswith("linux"):
    host_subpath = "linux-x86_64"
elif sys.platform.startswith("darwin"):
    host_subpath = "darwin-x86_64"
elif sys.platform.startswith("win"):
    if platform.machine().endswith("64"):
        host_subpath = "windows-x86_64"
    else:
        host_subpath = "windows"

def find_stl_lib_path(ndk_root, lib_arch_dir, triple_target_dir, host_subpath):
    # New NDK structure (NDK 23+)
    if host_subpath and triple_target_dir:
        new_path = f"{ndk_root}/toolchains/llvm/prebuilt/{host_subpath}/sysroot/usr/lib/{triple_target_dir}/libc++_shared.so"
        if os.path.isfile(new_path):
            print(f"Found libc++_shared.so using new NDK structure: {new_path}")
            return new_path
    
    # Old NDK structure (NDK 22 and earlier)
    old_path = f"{ndk_root}/sources/cxx-stl/llvm-libc++/libs/{lib_arch_dir}/libc++_shared.so"
    if os.path.isfile(old_path):
        print(f"Found libc++_shared.so using old NDK structure: {old_path}")
        return old_path
    
    return None

if lib_arch_dir != "":
    if env.dev_build:
        lib_type_dir = "dev"
    elif env.debug_features:
        if env.editor_build and env["store_release"]:
            lib_type_dir = "release"
        else:
            lib_type_dir = "debug"
    else:  # Release
        lib_type_dir = "release"

    if env.editor_build:
        lib_tools_dir = "tools/"
    else:
        lib_tools_dir = ""

    out_dir = "#platform/android/java/lib/libs/" + lib_tools_dir + lib_type_dir + "/" + lib_arch_dir
    env_android.Command(
        out_dir + "/libgodot_android.so", "#bin/libgodot" + env["SHLIBSUFFIX"], Move("$TARGET", "$SOURCE")
    )

    stl_lib_path = find_stl_lib_path(
        str(env["ANDROID_NDK_ROOT"]), 
        lib_arch_dir, 
        triple_target_dir, 
        host_subpath
    )
    
    if stl_lib_path:
        env_android.Command(out_dir + "/libc++_shared.so", stl_lib_path, Copy("$TARGET", "$SOURCE"))
    else:
        print_warning(f"Could not find libc++_shared.so for architecture {lib_arch_dir}")
        print_warning("Checked both old and new NDK directory structures")
        print_warning("Build may fail - please verify your NDK installation")

    def generate_apk(target, source, env):
        gradle_process = []

        if sys.platform.startswith("win"):
            gradle_process = [
                "cmd",
                "/c",
                "gradlew.bat",
            ]
        else:
            gradle_process = ["./gradlew"]

        gradle_process += [
            "generateGodotEditor" if env["target"] == "editor" else "generateGodotTemplates",
            "--quiet",
        ]

        if env["debug_symbols"]:
            gradle_process += ["-PdoNotStrip=true"]

        subprocess.run(
            gradle_process,
            cwd="platform/android/java",
        )

    if env["generate_apk"]:
        generate_apk_command = env_android.Command("generate_apk", [], generate_apk)
        command = env_android.AlwaysBuild(generate_apk_command)
        env_android.Depends(command, [lib])
